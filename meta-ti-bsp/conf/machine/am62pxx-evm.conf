#@TYPE: Machine
#@NAME: AM62PX EVM
#@DESCRIPTION: Machine configuration for the TI AM62PX EVM

require conf/machine/include/am62pxx.inc

KERNEL_DEVICETREE_PREFIX = " \
    ti/k3-am62p5 \
    ti/k3-am62x-sk-csi2 \
    ti/k3-am62x-sk-hdmi \
    ti/k3-am62x-sk-lpm \
    ti/k3-am62x-sk-mcspi-loopback \
    ti/k3-v3link \
"

KERNEL_DEVICETREE = " \
    ti/k3-am62p5-sk.dtb \
    ti/k3-am62x-sk-csi2-imx219.dtbo \
    ti/k3-am62x-sk-csi2-ov5640.dtbo \
    ti/k3-am62x-sk-csi2-tevi-ov5640.dtbo \
    ti/k3-am62x-sk-hdmi-audio.dtbo \
"

FIT_CONF_DEFAULT_DTB = "k3-am62p5-sk.dtb"

UBOOT_ECDSA_SIGN_CONFIG ?= "ecdsa"
UBOOT_ECDSA_SIGN_CONFIG:bsp-ti-6_6 = ""
UBOOT_ECDSA_SIGN_CONFIG:bsp-next = ""
UBOOT_ECDSA_SIGN_CONFIG:bsp-mainline = ""

ECDSA_BOOT_FILES ?= "tiboot3.bin-ecdsa tiboot3-*-evm.bin-ecdsa tispl.bin-ecdsa u-boot.img-ecdsa"
ECDSA_BOOT_FILES:bsp-ti-6_6 = ""
ECDSA_BOOT_FILES:bsp-next = ""
ECDSA_BOOT_FILES:bsp-mainline = ""

UBOOT_MACHINE = ""
UBOOT_CONFIG = "${UBOOT_ECDSA_SIGN_CONFIG} main"
UBOOT_CONFIG[main] = "am62px_evm_a53_defconfig"
UBOOT_CONFIG[ecdsa] = "am62px_evm_a53_defconfig"

UBOOT_CONFIG_MAKE_OPTS[ecdsa] = "${TI_SIGN_WITH_ECDSA_KEY}"

IMAGE_BOOT_FILES += "${ECDSA_BOOT_FILES}"

# UBOOT_FRAGMENTS holds the list of u-boot config fragments which has to be build
# along with the base defconfig mentioned in UBOOT_MACHINE. Refer u-boot-mergeconfig.inc
# under meta-ti-bsp/recipes-bsp/u-boot/ for more details.
# For AM62P tisdk-display-cluster image, splash screen is handled by SBL.
# Hence, disable the A53 based splash screen using the am62x_evm_prune_splashscreen.config fragment present in ti-u-boot tree
UBOOT_FRAGMENTS = "${@oe.utils.conditional('DISPLAY_CLUSTER_ENABLE', '1', 'am62x_evm_prune_splashscreen.config', '', d)}"
